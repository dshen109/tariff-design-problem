mcdp {
    provides recovery_rate [dimensionless]
    provides fixed_costs [USD]
    provides xsubsidy [USD]

    requires complexity [dimensionless]
    requires co2_budget [g]

    # perception = instance `perception
    cost_recovery = instance `cost_recovery
    tariff = instance `tariff
    subsidy = instance `subsidy
    practicality = instance `practicality
    emissions = instance `emissions

    # tariff block
    complexity_A required by tariff <= complexity_A provided by practicality
    complexity_B required by tariff <= complexity_B provided by practicality

    # subsidy block
    xsubsidy provided by subsidy >= provided xsubsidy
    provided fixed_costs <= fixed_costs provided by subsidy
    fixed_cost_frac provided by subsidy >= fixedfracA required by tariff
    purchase_cost provided by subsidy >= cost_energy_A required by tariff
    revenue required by subsidy <= revenue_A provided by tariff

    # cost recovery
    provided recovery_rate <= recovery_rate provided by cost_recovery
    provided fixed_costs <= cost_fixed provided by cost_recovery
    cost_energy_A provided by cost_recovery >= cost_energy_A required by tariff
    cost_energy_B provided by cost_recovery >= cost_energy_B required by tariff
    revenue_A required by cost_recovery <= revenue_A provided by tariff
    revenue_B required by cost_recovery <= revenue_B provided by tariff

    # practicality
    required complexity >= complexity required by practicality

    # emissions
    co2_budget required by emissions <= required co2_budget
    h0 provided by emissions >= T0_kWh required by tariff
    h1 provided by emissions >= T1_kWh required by tariff
    h2 provided by emissions >= T2_kWh required by tariff
    h3 provided by emissions >= T3_kWh required by tariff
    h4 provided by emissions >= T4_kWh required by tariff
}
